apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.7"

def siteUrl = 'http://takuma-nekomayoi-blog.logdown.com/'     // project homepage
def gitUrl = 'https://github.com/TakumaMochizuki/SimpleMaterialTabPager.git'  // project git
group = "com.github.takumalee"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0.7"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.0'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
}

install {
    repositories.mavenInstaller {
        // This generates POM. xml  with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'Simple to use Material tab with bar with Pager For Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'takumalee'
                        name 'takumalee'
                        email 'vmgsahm1@gmail.com'
                    }
                }
                scm { connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android. sourceSets .main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android. sourceSets .main.java.srcDirs
    classpath += project.files( android.getBootClasspath(). join ( File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    key = properties.getProperty("bintray.apikey")
    user = properties.getProperty("bintray.user")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "SimpleMaterialTabPager"                                                 // #CONFIG# project name in jcenter
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
